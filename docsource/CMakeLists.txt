set(CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}/cmake" ${CMAKE_MODULE_PATH})
find_package(Sphinx REQUIRED)

file(GLOB_RECURSE SPHINX_FILES ${CMAKE_CURRENT_LIST_DIR}/**/*.rst)

set(SPHINX_SOURCE ${CMAKE_CURRENT_LIST_DIR})
set(SPHINX_BUILD ${CMAKE_CURRENT_BINARY_DIR}/sphinx)
set(SPHINX_INDEX_FILE ${SPHINX_BUILD}/index.html)

add_custom_command(OUTPUT ${SPHINX_INDEX_FILE}
    COMMAND
    ${SPHINX_EXECUTABLE} -b html
    -Dbreathe_projects.mdk=${DOXYGEN_OUTPUT_DIR}/xml
    ${SPHINX_SOURCE} ${SPHINX_BUILD}
    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
    DEPENDS ${SPHINX_FILES}
    MAIN_DEPENDENCY ${SPHINX_SOURCE}/conf.py
    COMMENT "Generating documentation with Sphinx")

set(LOCAL_SPHINX_BUILD ${PROJECT_SOURCE_DIR}/docs)
set(LOCAL_SPHINX_INDEX ${LOCAL_SPHINX_BUILD}/index.html)

add_custom_command(OUTPUT ${LOCAL_SPHINX_INDEX}
    COMMAND
    ${CMAKE_COMMAND} -E rm -rf ${LOCAL_SPHINX_BUILD}
    COMMAND
    ${CMAKE_COMMAND} -E copy_directory ${SPHINX_BUILD}/ ${LOCAL_SPHINX_BUILD}
    COMMAND
    ${CMAKE_COMMAND} -E touch ${LOCAL_SPHINX_BUILD}/.nojekyll
    DEPENDS ${SPHINX_INDEX_FILE}
    COMMENT "Copying the generated docs to _build")

add_custom_target(Sphinx ALL DEPENDS ${LOCAL_SPHINX_INDEX})